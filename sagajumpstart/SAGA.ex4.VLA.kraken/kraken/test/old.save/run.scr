#!/bin/csh
#
# script to use kraken, field, and nbf to do MFP and generate 
# the ambiguity surfaces over specified Frequencies
#

if ($#argv != 1) then
  echo "Usage: run.scr f"
  echo "Where f has to be one of the following frequencies:"
  echo "49 64 79 94 112 130 148 166 201 235 283 338 388"
#  echo "49 112 201 388"
  exit
endif

set f = $1
echo $f

if (-e MODFIL) rm MODFIL
if (-e SHDFIL) rm SHDFIL

# set the source depths to loop through
set d1 = 2
set d2 = 200

set num_els = 21

# Make an array file for nbf... since we are using replica vectors,
# nbf does not need array element positions, but the program asks 
# for something.
@ i = $num_els * 4
const 0 $i array.sio
mchan $num_els array.sio array.sio

################################################################
###                 Running Kraken                           ###
################################################################
# edit kraken input file (input.env) for the correct frequency
ex input.env << INN
2c
$f
.
wq!
INN
# run kraken
kraken < input.env

################################################################
###                 Running field                            ###
################################################################
cp MODFIL MODFIL1

field < input.flp

set d = $d1
while ($d <= $d2)
krakp -R SHDFIL repvec.sio << INN
$d
INN

################################################################
###                 Running Nbf                              ###
################################################################

#     run the BARTLETT  case
      echo "=================================================================="
      echo "Freq = $f   SD = $d m  -- BARTLETT"
      echo "=================================================================="
      nbf -b c:1:0 -l fr:repvec.sio -f n:8192:8192:50:1500:${f} -p array.sio \
          -t traceout.${f}Hz.sio -x csdm.${f}Hz.sio nbfout.${f}Hz.sio

      if ($d == $d1) then
	mv nbfout.${f}Hz.sio amsurf.${f}Hz.sio
      else
	mux amsurf.${f}Hz.sio nbfout.${f}Hz.sio amsurf.${f}Hz.sio
      endif

## increment the depths by 2
#@ d = $d + 2
# increment the depths by 4
@ d = $d + 4
end

# divide by the trace of the input matrix to get correlations between 0 and 1
div -g -s traceout.${f}Hz.sio amsurf.${f}Hz.sio amsurf.${f}Hz.sio
logmag -d -z amsurf.${f}Hz.sio in.sio
# plot
ex pc << INN
20c
Conventional MFP, Frequency = $f Hz
.
wq!
INN
#shade -L -R -T -t pc in.sio amsurf.${f}Hz.ps
shade -L -R -T -t -r pc in.sio amsurf.${f}Hz.ps
